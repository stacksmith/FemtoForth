cd 'test

PROC head_name_display // (h--) print the qualified name
{ 0 swap // (0,h--) will be stacking recursively
  begin
    dup head'dad
    if( zr ) 
      drop begin
        head'name ctell
        if( zr ) drop ; thanx
        ''' emit
      repeat
    thanx
  repeat
}
end

// Decompiling a 0 token is tricky...
PROC deco1 // (ip--ip,e)
{
    U8'@++ // ptr,tok
    if( zr )
      drop ref ; head'owner ; // a search for "'system'core';" may be better?
    thanx
    4* over 1- table'base + @ // ip,ref
    if( head'owner nz )
        ;
    else
        drop 1 error'throw
    thanx
    ;
}
end

// decompile a token and associated data in the codestream
PROC deco_tok // (ip--ip)
{
    if( error'catch 0= )
        color'dim color'white '<' emit color'reset color'green
        deco1 dup head_name_display
        head'type 
        if( push "decompile" pop  head'locatex zr )
            dbl'drop 1 error'throw
        thanx
        color'dim color'white '>' emit color'reset color'white
        space
        head'code invoke // find and run decompile
        error'clear
    else 
        "ERROR" ctell 
    thanx
    ;
}
end
PROC deco_n // (ip,cnt--ip)
{ times (
// see if ip is a named target
  if( dup head'owner dup ) 
    color'cyan head'name ctell ':' emit cr
  else drop thanx
  
  color'green space dup . space // address
  color'cyan dup U8'@ hex2 space
  
  deco_tok 
  cr
  ) color'reset ;
}
end

cd 'system'TYPE'PROC
PROC decompile // (ip--ip)
{ ; }
end

cd 'system'TYPE'SYSVAR
PROC decompile // (ip--ip)
{ ; }
end

cd 'system'TYPE'POFF
PROC decompile // (ip--ip)
{ 
  U8'@++ over + .
; 
}
end

cd 'system'TYPE'PSTR8
PROC decompile // (ip--ip)
{   '"' emit
    U8'@++      // ptr,cnt
    dbl'dup ctell
    +
    '"' emit
    ;
}
end

cd 'system'TYPE'PU32
PROC decompile // (ip--ip)
{   
    dup @++ . ;
}
end

cd 'system'TYPE'PU16
PROC decompile // (ip--ip)
{   
    dup U16'@++ . ;
}
end

cd 'system'TYPE'PU8
PROC decompile // (ip--ip)
{   
    U8'@++ 
    dup hex2 space
    ''' emit
    emit 
    ''' emit
    ;
}
end
cd 'test

